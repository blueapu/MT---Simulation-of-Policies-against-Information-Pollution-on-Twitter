;;;; INFORMATION ------------------------------------------------------------------------------
;; Model: Countermeasure "Warning"
;; Version: warn_02 / Development: 04
;; Author: Markus Rottmann / 97-919-294
;; Date: 2021-04-07
;;
;; External Inputs:
;; 1) network-adjecency matrix "test_adj_matx01.txt"
;; 2) pobability to tweet (no, neutral, opposing, supporting) "prob_input_test03.txt"
;;
;; Internal Inputs (from Interface):
;; 1) duration of analysis "srv_d"
;; 2) number of initial tweets "no_ip_twt"
;; 3) life span of tweets "ls_twt"
;; 4) increase of probability to not tweet "p_warn"

;;;; SETUP ------------------------------------------------------------------------------
;; loading extensions (here: network)
extensions [ nw ]


;; defining variables
; defining global variables
globals
[
  prob_lst ; list of probabilities to tweet
  sum_newip ; sum of new tweets
]

; defining turtle (= twitter user) variables
turtles-own
[
  view_ip_twt ; indicator, if there is an information pollution tweet in the neighborhood (in one of the linked nodes)
  post_ip_twt ; indicator if turtle posts / posted an information pollution tweet
  tm_twt ; time since an tweet has been posted (in ticks)
  twt_type ; type of tweet posted (supporting, opposing, neutral)
  p_ip_NO  ; probability of posting no tweet (in percent)
  p_ip_SUP ; probability of posting a supporting tweet (in percent)
  p_ip_OPP ; probability of posting an opposing tweet (in percent)
  p_ip_NTR ; probability of posting a neutral tweet (in percent)
]


;; setting-up the stage
; defining sequence of setup-opertions
to setup
   set-current-directory "D:\\Studium\\MT_MasterThesis\\MT_Code\\NetLogo_Code"
   clear-all
   setup-network
   setup-tweets
   readin-prob
   setup-prob
   setup-countermeasures
   reset-ticks
end

; setup-network: setting-up network from network analysis
to setup-network
  set-default-shape turtles "circle"

  ; option "real network" (ntwrk = "real")
if ntwrk = "real" [
     nw:load-matrix "Data\\test_adj_matx01.txt" turtles links ; loading adjacency matrix
  ]

  ; option "less dense network" with an average of 15 links/turtle (ntwrk = "sim_15")
if ntwrk = "sim_15" [
     create-turtles 858
     let num-links (15 * 858) / 2  ; defining total number of links
     while [count links < num-links ] ; loop for number of links
       [
       ask one-of turtles ; instructing random turtle
         [
         let choice (min-one-of (other turtles with [not link-neighbor? myself])   ; defining the link turtle number "choice" as random from agentset. agentset = all neighbors that are no link neighbors with myself)...
                   [distance myself])                                              ; ... reporter = the agent number with the smallest ("min-one-of") of "distance to myself"
         if choice != nobody [ create-link-with choice ]                           ; ... if there is a turtle-number; create a link with this number
         ]
       ]
  ]

  ; option "less dense network" with an average of 30 links/turtle (ntwrk = "sim_30")
if ntwrk = "sim_30" [
     create-turtles 858
     let num-links (30 * 858) / 2  ; defining total number of links
     while [count links < num-links ] ; loop for number of links
       [
       ask one-of turtles ; instructing random turtle
         [
         let choice (min-one-of (other turtles with [not link-neighbor? myself])   ; defining the link turtle number "choice" as random from agentset. agentset = all neighbors that are no link neighbors with myself)...
                   [distance myself])                                              ; ... reporter = the agent number with the smallest ("min-one-of") of "distance to myself"
         if choice != nobody [ create-link-with choice ]                           ; ... if there is a turtle-number; create a link with this number
         ]
       ]
  ]

    ; option "less dense network" with an average of 45 links/turtle (ntwrk = "sim_45")
if ntwrk = "sim_45" [
     create-turtles 858
     let num-links (45 * 858) / 2  ; defining total number of links
     while [count links < num-links ] ; loop for number of links
       [
       ask one-of turtles ; instructing random turtle
         [
         let choice (min-one-of (other turtles with [not link-neighbor? myself])   ; defining the link turtle number "choice" as random from agentset. agentset = all neighbors that are no link neighbors with myself)...
                   [distance myself])                                              ; ... reporter = the agent number with the smallest ("min-one-of") of "distance to myself"
         if choice != nobody [ create-link-with choice ]                           ; ... if there is a turtle-number; create a link with this number
         ]
       ]
  ]





  repeat 30 [ layout-spring turtles links 0.3 ((world-width / sqrt count turtles) / 2) 2 ]
  ask turtles [ set label who ] ; labelling with turtle-id
  ask turtles [ set color grey ] ; setting basic turtle color
  ask turtles [ set size 1 ] ; setting turtle size
  ask turtles [ set twt_type "NO"] ; setting basic tweet ("NO" = no Tweet)
  ask turtles [ set tm_twt 0 ] ; setting time since tweet to zero
  ask turtles [ set post_ip_twt 0 ] ; setting indicator for information pollution tweets "0" (= no ip tweet)
end

; setup-tweets: setting-up initial number of tweets
to setup-tweets
    ask n-of no_ip_twt turtles
    [
    set post_ip_twt 1
    set twt_type "NTR"
    set color blue
    set tm_twt 0
    set sum_newip 0
    ]
end

; readin-prob: reading-in basic predicted probabilies from tweet analyis (used in setup-prob)
to readin-prob

    ifelse ( file-exists? "Data\\prob_input_test03.txt" ) ; check, if file "prob_input_01.txt" exists in specified folder
    [
     set prob_lst [] ; preparing a list for probabilities

    file-open "Data\\prob_input_test03.txt" ; opening the data

       while [ not file-at-end? ] ; reading the file to the end
         [
          set prob_lst sentence prob_lst file-read ; reads-in all probabiliies in one single list
         ]

   ;   user-message "Input file prob_input_test03.txt sucessfully loaded!" ; message when sucessfully loaded
      file-close ; closing file
    ]

    [
    user-message "There is no file called prob_input_test03.txt in directory Data!" ; message, when no file "prob_input_test00.txt" present ("else"-condition of first "ifelse")
    ]
end

; setup-prob: assigning read-in predicted probabilities to each twitter user
to setup-prob
  ask turtles [
    set p_ip_NO item  ((who * 4) + 0) prob_lst ; every first (+ 0) entry is probability "NO"
    set p_ip_NTR item ((who * 4) + 1) prob_lst ; every second entry (+ 1) is probability "NTR"
    set p_ip_OPP item ((who * 4) + 2) prob_lst ; every third entry (+ 2) is probability "OPP"
    set p_ip_SUP item ((who * 4) + 3) prob_lst ; every fourth entry (+ 3) is probability "SUP"
  ]
end

; Setup-countermeasures: implementing countermeasures
to setup-countermeasures
    ask turtles [

      ; calculating auxillary varaibles
      let p_warneff ( (100 - p_ip_NO) * (p_warn / 100) ) ; calc. effect of warning
      let p_ip_YES (100 - p_ip_NO) ; calc. prob. of posting ip_tweet

      ; test if p_ip_YES is 0. If yes: all set to simple settings
      if p_ip_YES = 0 [
                      set p_ip_NO 100
                      set p_ip_NTR 0
                      set p_ip_OPP 0
                      set p_ip_SUP 0
                      ]

      ; test if p_ip_YES is 0. If NO: calculate remaining probabilities
      if p_ip_YES != 0 [
                        let r_NTR (p_ip_NTR / p_ip_YES) ; calc. ratio of NTR ip tweets
                        let r_OPP (p_ip_OPP / p_ip_YES) ; calc. ratio of OPP ip tweets
                        let r_SUP (p_ip_SUP / p_ip_YES) ; calc. ratio of SUP ip tweets

                        set p_ip_NO (p_ip_NO + p_warneff) ; calc. of new prob of NO ip tweet
                        set p_ip_NTR (p_ip_NTR - (p_warneff * r_NTR) ) ; calc. of new prob. of NTR ip tweets
                        set p_ip_OPP (p_ip_OPP - (p_warneff * r_OPP) ) ; calc. of new prob. of OPP ip tweets
                        set p_ip_SUP (p_ip_SUP - (p_warneff * r_SUP) ) ; calc. of new prob. of OPP ip tweets
                        ]
                   ]
end

;;;; EXECUTION ---------------------------------------------------------------------------
;; defining sequence of execution operations
to go
   tweet_action
   tweet_cleanup
   if ticks >= srv_d [                         ; stopping & reporting after no of ticks
                      output-print sum_newip
                      output-print count turtles
                      output-print ticks
                      stop
                      ]
   if count turtles with [ post_ip_twt = 1 ] <= 0 [ ; stopping & reporting after no of ticks
                      output-print sum_newip
                      output-print count turtles
                      output-print ticks
                      stop ]
   tick
end

;; executing operations
; tweet_action: turtle tweets based on: 1) neighboring tweet, 2) assigned probabilites
to tweet_action
    ask turtles
    [

      if post_ip_twt = 1 [ set tm_twt tm_twt + 1 ] ; tweet aging: counting days since tweet one up

      let friends link-neighbors ; defining linked neighbors as "friends"
      let ip_cnt count friends with [ post_ip_twt = 1 ] ; defining "ip_cnt" as number of "friends" with tweeets (post_ip_twt = "1").

      if ip_cnt > 0  [; if there are any "friends" with tweets, the following applies

      ;; block assigning Tweet Types based on probabilities. NOTE: all values x 100 for capturing sub-1 probabilities.
      let rdm_no random 10000

        if rdm_no <= (100 * p_ip_NO) [set twt_type "NO"] ; in p_ip_NO there will be no Tweet

        if rdm_no > (100 * p_ip_NO) and rdm_no <= (100 * (p_ip_NO + p_ip_NTR)) [
                                                                set twt_type "NTR"  ; in "p_ip_NTR" percent of all cases, the following applies
                                                                set sum_newip ( sum_newip + 1 )
                                                                set post_ip_twt 1
                                                                set tm_twt 0 ] ;

        if rdm_no > (100 * (p_ip_NO + p_ip_NTR)) and rdm_no <= (100 * (p_ip_NO + p_ip_NTR + p_ip_OPP)) [
                                                                set twt_type "OPP"  ; in "p_ip_OPP" percent of all cases, the following applies
                                                                set sum_newip ( sum_newip + 1 )
                                                                set post_ip_twt 1
                                                                set tm_twt 0 ]


        if rdm_no > (100 * (p_ip_NO + p_ip_NTR + p_ip_OPP)) and rdm_no <= 10000 [
                                                                set twt_type "SUP"  ; in "p_ip_SUP" percent of all cases, the following applies
                                                                set sum_newip ( sum_newip + 1 )
                                                                set post_ip_twt 1
                                                                set tm_twt 0 ]
                                       ]
  ]
end

; tweet_cleanup: ip tweets that reached tweet lifespan "ls_twt" (from interface) are set-back to "NO" (= no ip-tweet)
to tweet_cleanup
ask turtles
    [
      if tm_twt >= ls_twt ; checking if lifespan "ls_twt" has been reached
        [
        set tm_twt 0 ; if yes, setting back time since tweet
        set post_ip_twt 0 ; if yes, setting back tweet indicator to "zero"
        set twt_type "NO" ; if yes; lable twitterer as "NO"
        ]

      if twt_type = "NTR" [ set color blue ] ; change colors acc. to tweet type
      if twt_type = "SUP" [ set color red ] ; change colors acc. to tweet type
      if twt_type = "OPP" [ set color green ] ; change colors acc. to tweet type
      if twt_type = "NO"  [ set color grey ] ; change colors acc. to tweet type
    ]
end